-- LEONARDO MACHADO BAPTISTA 07.05.24
-- REALIZA UNI√ÉO DE TABELAS DE DIFERENTES EMPRESAS, TENDO COMO BASE A EMPRESA 01.

-----------------------------------------------------------
/* 
***********************************
******* RESGATAR METADADOS ********
***********************************
*/

DECLARE @TABELAS TABLE (NOME VARCHAR(11));
INSERT INTO @TABELAS
SELECT
    TABLE_NAME
FROM INFORMATION_SCHEMA.TABLES WITH(NOLOCK)
WHERE 
    TABLE_NAME LIKE @TABELA_PROTHEUS+'%' AND
    TABLE_NAME NOT LIKE '%9__%' AND
    TABLE_NAME NOT LIKE '%LOG%' AND
    LEN(TABLE_NAME) = '6'
ORDER BY TABLE_NAME ASC

DECLARE @CREATE_TEMP_TABLE_VALUES NVARCHAR(MAX) = N'';
SELECT @CREATE_TEMP_TABLE_VALUES += 
	QUOTENAME(COLUMN_NAME) + ' ' + 
	(CASE WHEN CHARACTER_MAXIMUM_LENGTH IS NULL THEN DATA_TYPE ELSE DATA_TYPE + '(' + CAST(CHARACTER_MAXIMUM_LENGTH AS VARCHAR(MAX)) + ')' END) 
	+ ' NULL,'
FROM INFORMATION_SCHEMA.COLUMNS
WHERE 
	TABLE_NAME = @TABELA_PROTHEUS+'010' AND
	DATA_TYPE NOT IN ('image', 'varbinary')
SET @CREATE_TEMP_TABLE_VALUES = LEFT(@CREATE_TEMP_TABLE_VALUES, LEN(@CREATE_TEMP_TABLE_VALUES) - 1);

DECLARE @CREATE_TEMP NVARCHAR(MAX) = 
	N'DROP TABLE IF EXISTS ##T_' + @TABELA_PROTHEUS + '_DW_SEL;' + 
	'CREATE TABLE ##T_' + @TABELA_PROTHEUS + '_DW_SEL' + 
	'
	(
		'+ @TABELA_PROTHEUS +'_EMPRESA VARCHAR(2) NULL,
		'+ @CREATE_TEMP_TABLE_VALUES +'
	);
	'
EXEC sp_executesql @CREATE_TEMP
-----------------------------------------------------------

DECLARE @LISTA_COLUNAS NVARCHAR(MAX) = N'';
DECLARE @NOME NVARCHAR(15);
DECLARE PROCURA CURSOR
    LOCAL STATIC READ_ONLY FORWARD_ONLY
FOR
    SELECT * FROM @TABELAS

OPEN PROCURA
FETCH NEXT FROM PROCURA INTO @NOME
WHILE @@FETCH_STATUS = 0
BEGIN
	SET @LISTA_COLUNAS = N'';
	SELECT 
		@LISTA_COLUNAS += QUOTENAME(COLUMN_NAME) + ','
	FROM INFORMATION_SCHEMA.COLUMNS
	WHERE 
		TABLE_NAME = @NOME AND
		DATA_TYPE NOT IN ('image', 'varbinary')
	SET @LISTA_COLUNAS = LEFT(@LISTA_COLUNAS, LEN(@LISTA_COLUNAS) - 1);

   DECLARE @SELECT NVARCHAR(MAX) = N'
	INSERT INTO ##T_' + @TABELA_PROTHEUS + '_DW_SEL
	(' + (SELECT SUBSTRING(@NOME, 1, 3)) + '_EMPRESA, ' + @LISTA_COLUNAS + ')
	SELECT
		''' + (SELECT SUBSTRING(@NOME, 4, 2)) + ''' AS ' + (SELECT SUBSTRING(@NOME, 1, 3)) + '_EMPRESA,
		'+ @LISTA_COLUNAS +'
	FROM ' + @NOME +  ' WITH(NOLOCK);
   '

    EXEC sp_executesql @SELECT
    FETCH NEXT FROM PROCURA INTO @NOME
END
CLOSE PROCURA
DEALLOCATE PROCURA;

DECLARE @CONSULTA NVARCHAR(MAX) = 
    N'SELECT *, GETDATE(), GETDATE() FROM ##T_' + @TABELA_PROTHEUS + '_DW_SEL
    DROP TABLE IF EXISTS ##T_' + @TABELA_PROTHEUS + '_DW_SEL';
EXEC sp_executesql @CONSULTA;